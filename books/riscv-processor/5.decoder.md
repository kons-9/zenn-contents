---
title: "decoderを作ってみよう
---
# decoderの作り方
まずはRV32Iに対応したdecoderを作成します。
このセクションが終わると、以下のようなディレクトリ構造になります。
```
% tree             
.
├── Makefile
├── README.md
├── include
│   ├── color.svh
│   ├── decoder_type.svh
│   ├── test_utils.svh
│   └── test_utils_impl.svh
├── src
│   └── decoder.sv
└── test
    └── tb_decoder.sv

4 directories, 8 files
```

## デコーダとは
デコーダは、命令を受け取り、その命令が何を意味しているのかを解釈する機能を持ちます。
命令は32bitのビット列で表現されますが、このままでは人間にとって理解しにくいです。
そこで、デコーダは32bitのビット列を、わかりやすい信号線に変換します。
この様な変換を行うことで、CPUの設計が容易になります。

その他、デコーダは以下の機能を持つことがあります。
* 処理の高速化
* エラー処理

パイプライン化する場合には、解釈に時間がかかる部分を前もって行うことで、処理を高速化することができます。
デコーダで処理可能なエラーに関しては前もってエラーを検出することで、副作用を最小限に抑えることができます。

## RV32Iのデコード

### U-Typeのデコード

手始めにU-typeの命令をデコードする機能を作成します。
RV32Iには様々な命令がありますが、formatは限られており以下の6つです。
* R-Type
* I-Type
* S-Type
* B-Type
* U-Type
* J-Type

なにから初めてもよかったのですが、気分的にU-Typeから初めてみます。
U-Typeは命令列32bitのうち、上位20bitが即値に使われ、次の5bitがrd、最後の7bitがopcodeとなります。
即値の取り出し方は31bit目から12bit目までのビットを上部に格納した32bitの即値として取り出すことになります。

U-Typeの命令の一つにLUI命令があります。
LUI命令を用いてテストベンチを表現します。
```systemverilog
// tb_decoder.sv
`include "decoder_type.svh"
`include "test_utils.svh"
module tb_decoder();
    decoder_type::decoder_input_t decoder_input;
    decoder_type::decoder_output_t decoder_output;

    decoder decoder_inst(
        .decoder_input,
        .decoder_output
    );
    decoder_type::decoder_output_t expected_output;

    initial begin
        `TEST_START("decoder.log");
        // LUI命令
        // lui t0, 0x8000
        decoder_input.instr = 32'h080002b7; // lui     t0,0x8000
        expected_output.opcode = 7'b0110111;
        expected_output.rd = 5'b00101;
        expected_output.imm = 32'h8000000;
        #1;
        `TEST_EXPECTED(expected_output.opcode, decoder_output.opcode, "lui opcode");
        `TEST_EXPECTED(expected_output.rd, decoder_output.rd, "lui rd");
        `TEST_EXPECTED(expected_output.imm, decoder_output.imm, "lui imm");

        `TEST_RESULT();
        $finish;
    end
endmodule
```

このテストベンチを実行すると、コンパイルエラーが発生します。
これは、まだdecoderモジュールが存在しないためです。
早速decoderモジュールを作成します。
```
$ mkdir -p src
$ touch src/decoder.sv
```

decoderモジュールの中身は以下の通りです。
ここで、即値は符号拡張されていることに注意してください。
```systemverilog
// src/decoder.sv
`include "decoder_type.svh"
module decoder(
    input decoder_type::decoder_input_t decoder_input,
    output decoder_type::decoder_output_t decoder_output
);
    assign decoder_output.opcode = decoder_input.instr[6:0];
    assign decoder_output.rd = decoder_input.instr[11:7];
    assign decoder_output.imm = {decoder_input.instr[31:12], 12'b0};
endmodule
```
まだテストベンチを実行すると、コンパイルエラーが発生します。
これはincludeするファイルが存在しないためです。
decoderモジュールで使用する`decoder_type.svh`を作成します。
```systemverilog
// include/decoder_type.svh
`ifndef DECODER_TYPE_SVH
`define DECODER_TYPE_SVH
package decoder_type;
    typedef struct packed {
        logic [31:0] instr;
    } decoder_input_t;
    typedef struct packed {
        logic [6:0] opcode;
        logic [4:0] rd;
        logic [19:0] imm;
    } decoder_output_t;
endpackage
`endif // DECODER_TYPE_SVH
```
これでテストベンチを実行すると、全てのテストが通過するはずです。

さあ、次のU-Typeの命令をデコードする機能を追加してみましょう。
他のU-Typeの命令として、AUIPC命令があります。
AUIPC命令も即値をレジスタに格納する命令ですが、現在のPCの値を加算して格納します。
ただし、加算はデコードステージでは行わず、実行ステージで行います。

早速AUIPC命令をデコードするテストを追加します。

```systemverilog
// test/tb_decoder.sv
`include "decoder_type.svh"
`include "test_utils.svh"
module tb_decoder();
...
    initial begin
        `TEST_START("decoder.log");
        // LUI命令
        // lui t0, 0x8000
        decoder_input.instr = 32'h080002b7; // lui     t0,0x8000
        expected_output.opcode = 7'b0110111;
        expected_output.rd = 5'b00101;
        expected_output.imm = 32'h8000000;
        #1;
        `TEST_EXPECTED(expected_output.opcode, decoder_output.opcode, "lui opcode");
        `TEST_EXPECTED(expected_output.rd, decoder_output.rd, "lui rd");
        `TEST_EXPECTED(expected_output.imm, decoder_output.imm, "lui imm");
        #1;

        // AUIPC命令
        // auipc t1, 0x8000
        decode_input.instr = 32'h08000317; // auipc   t1,0x8000
        expected_output.opcode = opcode_types::AUIPC;
        expected_output.rd     = 6;
        expected_output.imm    = 32'h8000000; // 0x8000 << 12
        #1;
        `TEST_EXPECTED(expected_output.opcode, decode_output.opcode, "auipc");
        `TEST_EXPECTED(expected_output.rd, decode_output.rd, "auipc");
        `TEST_EXPECTED(expected_output.imm, decode_output.imm, "auipc");
        #1;


        `TEST_RESULT();
        $finish;
    end
...
endmodule
```
このテストベンチを実行すると実行が成功するはずです。
これは、現在出力としているAUIPC命令の構造とLUI命令の構造が一緒だからです。

## J-Typeのデコード
次にJ-Typeのデコードを行います。
// TODO図を引っ張る
J-Typeの命令は
* opcode: 6bit
* rd: 5bit
* imm: 20bit
となります。

J-Typeの命令の一つにJAL命令があります。
JAL命令を用いてテストベンチを表現します。
```systemverilog
// test/tb_decoder.sv
...
    initial begin
        ...
        decoder_input.instr = 32'hff9ff2ef; // jal     t0,0 <_start>
        // this operation address is 0x8
        expected_output.opcode = 7'b1101111;
        expected_output.rd     = 5;
        expected_output.imm    = -8;
        #1;
        // J-Type
        `TEST_EXPECTED(expected_output.opcode, decoder_output.opcode, "jal");
        `TEST_EXPECTED(expected_output.rd, decoder_output.rd, "jal");
        `TEST_EXPECTED(expected_output.imm, decoder_output.imm, "jal");
        #1;
```

そうすると、immに関してのテストが失敗します。
これはU-TypeとJ-Typeでimmの取り出し方が異なるためです。

これに対応するために、decoderモジュールを以下のように修正します。
```systemverilog
// decoder.sv
`include "decoder_type.svh"
module decoder(
    input decoder_type::decoder_input_t decoder_input,
    output decoder_type::decoder_output_t decoder_output
);
    parameter logic [6:0] JAL_OPCODE = 7'b1101111;

    assign decoder_output.opcode = decoder_input.instr[6:0];
    assign decoder_output.rd = decoder_input.instr[11:7];
    always_comb begin
        case (decoder_output.opcode)
            // jal
            JAL_OPCODE: begin
                decoder_output.imm = {
                    decoder_input.instr[31],
                    decoder_input.instr[19:12],
                    decoder_input.instr[20],
                    decoder_input.instr[30:21],
                    1'b0
                };
            end
            default: begin
                decoder_output.imm = {decoder_input.instr[31:12], 12'b0};
            end
        endcase
    end
endmodule
```
これでテストベンチを実行すると、以下のようなエラーが発生します。
```
NAME --timing --binary --main -Iinclude src/decoder.sv test/tb_decoder.sv --top-module tb_decoder -Mdir build
%Warning-UNOPTFLAT: test/tb_decoder.sv:6:36: Signal unoptimizable: Circular combinational logic: 'tb_decoder.decoder_output'
    6 |     decoder_type::decoder_output_t decoder_output;
      |                                    ^~~~~~~~~~~~~~
                    ... For warning description see https://verilator.org/warn/UNOPTFLAT?v=5.032
                    ... Use "/* verilator lint_off UNOPTFLAT */" and lint_on around source to disable this message.
                    test/tb_decoder.sv:6:36:      Example path: tb_decoder.decoder_output
                    src/decoder.sv:12:5:      Example path: ALWAYS
                    test/tb_decoder.sv:6:36:      Example path: tb_decoder.decoder_output
%Error: Exiting due to 1 warning(s)
make: *** [Makefile:51: build/Vtb_decoder] エラー 1
```

これは、循環結線が発生しているというエラーです。
循環結線とは、循環した結線が存在することを指します。循環していると結果が不定になる可能性があります。
これは論理的には循環結線ではないです。
しかし、verilatorはstructに対する操作をまとめて検証するため、decoder_outputの中のimmをdecode_outputのopcodeを用いて変更すると、decoder_output全体がdecode_outputに依存すると判断されてしまいます。((一昔まえのrustの所有権に関するコンパイルエラーに似ていると感じました。))

このエラーを回避するために、opcodeを一時変数に格納してからimmを変更するようにします。
```systemverilog
// decoder.sv
`include "decoder_type.svh"
module decoder(
    input decoder_type::decoder_input_t decoder_input,
    output decoder_type::decoder_output_t decoder_output
);
    logic [6:0] opcode;
    assign opcode = decoder_input.instr[6:0];

    parameter logic [6:0] JAL_OPCODE = 7'b1101111;

    assign decoder_output.opcode = opcode;
    assign decoder_output.rd = decoder_input.instr[11:7];
    always_comb begin
        case (opcode)
            // jal
            JAL_OPCODE: begin
                decoder_output.imm = {
                    decoder_input.instr[31],
                    decoder_input.instr[19:12],
                    decoder_input.instr[20],
                    decoder_input.instr[30:21],
                    1'b0
                };
            end
            default: begin
                decoder_output.imm = {decoder_input.instr[31:12], 12'b0};
            end
        endcase
    end
endmodule

```

これでテストベンチを実行すると、全てのテストが通過するはずです。

## ちょっとリファクタリング
decoderモジュールやテストベンチが読みにくくなってきたので、リファクタリングを行います。


